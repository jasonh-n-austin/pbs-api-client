apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

sourceCompatibility = 1.5

repositories {
    mavenCentral()
    maven {
        url "http://maven2.gueck.com/releases"
    }
}

jar {
    baseName 'pbs-api-client'
    version '1.2.1'
    manifest {
        attributes 'pbs-api-client': 'Paperbackswap.com API Client',
                'Implementation-Version': version
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'org.paperbackswap'
            artifactId 'pbs-api-client'
            version '1.2.1'

            from components.java

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'API Client/SDK for Paperbackswap.com')
                root.appendNode('description', 'Provides OAuth, URLs, and deserialization of PBS API')
                root.appendNode('url', 'https://github.com/jasonh-n-austin/pbs-api-client')
                root.appendNode('inceptionYear', '2014')
                def scm = root.appendNode('scm')
                scm.appendNode('url', 'https://github.com/jasonh-n-austin/pbs-api-client')
                scm.appendNode('connection', 'scm:https://jasharmon@github.com/jasonh-n-austin/pbs-api-client.git')
                scm.appendNode('developerConnection', 'scm:git://github.com/jasonh-n-austin/pbs-api-client.git')
                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The MIT License (MIT)')
                license.appendNode('url', 'http://www.opensource.org/licenses/mit-license.php')
                license.appendNode('distribution', 'repo')
                def developer = root.appendNode('developers').appendNode('developer')
                developer.appendNode('id', 'jasonh-n-austin')
                developer.appendNode('name', 'Jason Harmon')
                developer.appendNode('email', 'jason.harmon@gmail.com')
            }
        }
    }
}


dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile group: 'oauth.signpost', name: 'signpost-core', version: '1.2.1.2'
    compile group: 'oauth.signpost', name: 'signpost-commonshttp4', version: '1.2.1.2'
    compile group: 'mikaelhg', name: 'urlbuilder', version: '1.3.2'
    compile group: 'org.json', name: 'json', version: '20140107'
    compile group: 'com.google.inject', name: 'guice', version: '3.0'
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
}